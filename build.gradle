buildscript {
    repositories {
        maven { url = 'https://nexus.mcbans.com/repository/minecraft-forge/' }
        maven { url = 'https://nexus.mcbans.com/repository/modded_mc/'}
        maven { url = 'https://nexus.mcbans.com/repository/maven-central/' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}
plugins {
    // https://github.com/matthewprenger/CurseGradle
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.github.johnrengelman.shadow" version "7.1.0"
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'
apply plugin: 'maven-publish'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

version = mod_version
group = mod_packagename
archivesBaseName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
    mappings channel: 'official', version: mod_minecraft_version

    runs {
        client {
            workingDirectory project.file('run').canonicalPath
            property 'forge.logging.console.level', 'debug'
            args = ['--username', 'henkelmax']
            source sourceSets.main
            mods {
                mod_id {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server').canonicalPath
            property 'forge.logging.console.level', 'debug'
            source sourceSets.main
            mods {
                mod_id {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                mod_id {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    mavenLocal()
    maven { url = 'https://nexus.mcbans.com/repository/maven-central/' }
    maven { url = 'https://nexus.mcbans.com/repository/modded_mc/'}
    maven {
        name = "Progwml6 maven"
        url = "https://nexus.mcbans.com/repository/Progwml6/"
    }
    maven {
        name = "ModMaven"
        url = "https://nexus.mcbans.com/repository/ModHaven/"
    }
    maven {
        name 'tterrag maven'
        url "https://nexus.mcbans.com/repository/tterrag/"
    }
    maven {
        url "https://nexus.mcbans.com/repository/curse/"
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${mod_minecraft_version}-${forge_version}"

    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}")

    compileOnly "mekanism:Mekanism:${mekanism_version}:api"

    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:additions")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:generators")
    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}:tools")

    implementation fg.deobf("curse.maven:the-one-probe-245211:${theoneprobe_id}")

    implementation fg.deobf("curse.maven:jade-324717:${jade_id}")

    shadow fg.deobf("de.maxhenkel.corelib:corelib:${mod_minecraft_version}-${corelib_version}:api")
    runtimeOnly fg.deobf("de.maxhenkel.corelib:corelib:${mod_minecraft_version}-${corelib_version}")
    runtimeOnly fg.deobf("de.maxhenkel.corelib:corelib:${mod_minecraft_version}-${corelib_version}:javadoc")

    implementation 'com.google.code.findbugs:jsr305:3.0.2'
}

jar {
    manifest {
        attributes(["Specification-Title"     : mod_name,
                    "Specification-Vendor"    : mod_vendor,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : mod_name,
                    "Implementation-Version"  : mod_version,
                    "Implementation-Vendor"   : mod_vendor,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

processResources {
    filesMatching("**/*.toml") {
        expand 'mod_id': mod_id,
                'mod_version': mod_version,
                'mod_vendor': mod_vendor,
                'mod_name': mod_name,
                'mod_forge_version': mod_forge_version,
                'mod_loader_version': mod_loader_version,
                'mod_minecraft_version': mod_minecraft_version,
                'mod_mekanism_version_range': mod_mekanism_version_range,
                'jei_version_range': jei_version_range
    }
}


javadoc {
    failOnError = false
}

task deobfJar(type: Jar) {
    classifier 'deobf'
    from sourceSets.main.output
    manifest {
        attributes(["Specification-Title"     : mod_name,
                    "Specification-Vendor"    : mod_vendor,
                    "Specification-Version"   : "1",
                    "Implementation-Title"    : mod_name,
                    "Implementation-Version"  : mod_version,
                    "Implementation-Vendor"   : mod_vendor,
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")],)
    }
}

task sourcesJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.getDestinationDir()
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

jar.finalizedBy('reobfJar')

shadowJar {
    project.configurations.shadow.setTransitive(true);
    configurations = [project.configurations.shadow]
    classifier ""
    relocate 'de.maxhenkel.corelib', "de.maxhenkel.${mod_id}.corelib"
}

reobf {
    shadowJar {}
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = mod_packagename
            artifactId = mod_id
            version = mod_version

            artifact jar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            credentials {
                username = "jenkins"
                password = "$System.env.jenkins_password"
            }
            name = 'henkelmax.public'
            url = 'https://nexus.mcbans.com/repository/modded_mc/'
        }
    }
}